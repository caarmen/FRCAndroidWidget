apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "ca.rmen.android.frenchcalendar"
        minSdkVersion 3
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        buildConfigField 'String', 'GIT_SHA1', '"' + gitSha1() + '"'
        buildConfigField 'String', 'BUILD_DATE', '"' + buildDate() + '"'
        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'

    }

    productFlavors {
        foss {
            buildConfigField 'boolean', 'FOSS', 'true'
        }
        wear {
            buildConfigField 'boolean', 'FOSS', 'false'
        }
    }

    if(project.hasProperty("keyStore")) {
        signingConfigs {
            release {
                storeFile file(project.keyStore)
                storePassword project.keyStorePassword
                keyAlias project.keyAlias
                keyPassword project.keyAliasPassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else {
        android.buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

android.applicationVariants.all { variant ->
    // Perform resource filtering
    variant.mergeResources.doLast {
        filterResources(project, variant)
    }
}

dependencies {
    compile 'ca.rmen:lib-french-revolutionary-calendar:1.5.1'
    compile 'com.robbypond:android-ColorPickerPreference:1.11.1'
    compile 'com.vanniktech:vntfontlistpreference:1.0.0'

    compile project(':common')

    // Package the wear apk inside this one (only for the 'wear' flavor)
    if(rootProject.findProject("wearable") != null) {
        wearCompile ('com.google.android.gms:play-services-wearable:9.6.1') {
            exclude group: 'com.google.firebase', module: 'firebase-common'
        }
        wearWearApp project(':wearable')
    }
    compile "com.android.support:support-compat:${rootProject.supportLibVersion}"
    compile "com.android.support:support-annotations:${rootProject.supportLibVersion}"
}

