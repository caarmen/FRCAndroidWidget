apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "ca.rmen.android.frenchcalendar"
        minSdkVersion 3
        targetSdkVersion 26
        versionCode 1181
        versionName "1.18.1"
        buildConfigField 'String', 'GIT_SHA1', '"' + gitSha1() + '"'
        buildConfigField 'String', 'BUILD_DATE', '"' + buildDate() + '"'
        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
        return this;
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    if(project.hasProperty("keyStore")) {
        signingConfigs {
            release {
                storeFile file(project.keyStore)
                storePassword project.keyStorePassword
                keyAlias project.keyAlias
                keyPassword project.keyAliasPassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else {
        android.buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    return this;
}

android.applicationVariants.all { variant ->
    // Perform resource filtering
    variant.mergeResources.doLast {
        filterResources(project, variant)
    }
}

dependencies {
    compile 'ca.rmen:lib-french-revolutionary-calendar:1.8.0'
    compile 'com.robbypond:android-ColorPickerPreference:1.11.1'
    compile 'com.vanniktech:vntfontlistpreference:1.0.0'

    compile "com.android.support:support-annotations:${rootProject.supportLibVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

